# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=uuterm-svn
pkgver=80
pkgrel=2
pkgdesc="a portable, minimal VT100-like terminal emulator with maximal Unicode support"
arch=('i686' 'x86_64')
url="http://www.etalabs.net/uuterm.html"
license=('GPL2')
depends=('libx11' 'ncurses')
makedepends=('subversion' 'sh')
provides=('uuterm')
conflicts=('uuterm')
install=uuterm.install
source=(uuterm::svn://svn.mplayerhq.hu/uuterm/trunk/)
md5sums=('SKIP')

build() {
  cd uuterm

  # honor build flags
  for flags in {C,LD}FLAGS; do
    [[ ${!flags} ]] && printf '%s = %s\n' "$flags" "${!flags}"
  done > config.mak

  # default target only builds uuterm-x11
  make all uuterm-fb
}

package() {
  cd uuterm

  # compile terminfo
  install -dm755 "$pkgdir/usr/share/terminfo"
  tic -o "$pkgdir/usr/share/terminfo" uuterm.ti

  # ucf is the new hotness
  install -Dm644 ytty/ytty.ucf "$pkgdir/usr/share/fonts/ucf/ytty.ucf"

  install -Dm755 uuterm-x11 "$pkgdir/usr/bin/uuterm-x11"
  install -Dm755 uuterm-fb "$pkgdir/usr/bin/uuterm-fb"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/uuterm/COPYING"
}
