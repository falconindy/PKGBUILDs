# Contributor: Dave Reisner <d@falconindy.com>
pkgname=systemd-git
pkgver=20100524
pkgrel=2
pkgdesc="systems Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-core' 'dbus-glib' 'kernel26-systemd' 'glibc' 'libcgroup-git' 'udev>=154' 'libcap')
makedepends=('git' 'vala' 'gtk2' 'docbook-xsl')
optdepends=('gtk2: for systemadm GUI')
provides=('sysvinit')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # If gtk2 is found on the system, the graphical utilities will be
  # built. This can be explicitly disabled with --disable-gtk.
  ./bootstrap.sh --prefix=/usr --libexecdir=/usr/lib || return 1

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install || return 1
  cd "$pkgdir"
  mkdir -p cgroup/{debug,cpuset,ns,net_cls,cpu,cpuacct,memory,devices,freezer}
}
